@precedence {
	member,
	call,
	unary,
	rel @left,
	inv,
	exp @left,
	times @left,
	plus @left,
	cmp @left,
	bit @left,
	ternary @right,
	assign @right
}

// expression must be delimited by ";" or EOF
@top Script { statement }

@skip {} {
	// String may contain newlines
	String[isolate] {
		'"' (stringContentDouble | escape)* '"' |
		"'" (stringContentSingle | escape)* "'"
	}

	// Block comments only
	Comment[isolate] { "/*" commentContent* commentEnd }
}

@local tokens {
	commentEnd { "*/" }
	@else commentContent
}

@skip { spaces | newline | Comment }

statement { expression? (";" expression?)* }

expression[@isGroup=Expression] {
	// Simple expressions
	String |
	boolean { @specialize[@name=Bool]<identifier, "true" | "false"> } |
	kw<"null"> |

	// external tokens
	Num |
	VarName |
	DeprecatedVar |
	DisabledVar |
	GlobalVar |

	// Complex expressions
	ArrayExpression { "[" commaSep "]" } |
	ParenthesizedExpression { "(" statement ")" } |
	UnaryExpression |
	MemberExpression |
	BinaryExpression |
	IfStatement { kw<"if"> expression kw<"then"> expression (kw<"else"> expression)? kw<"end"> } |
	ConditionalExpression { expression !ternary LogicOp<"?"> expression LogicOp<":"> expression } |
	AssignmentExpression { (VarName | MemberExpression) !assign ":=" expression } |

	// external token
	CallExpression { (Func | Callee) !call "(" commaSep ")" }
}

kw<term> { @specialize[@name={term}]<identifier, term> }

// allow trailing comma but not sparse items
commaSep { "" | expression ("," expression)* ","? }

UnaryExpression {
	!unary plusMin expression |
	!inv LogicOp<"!"> expression
}

plusMin { ArithOp<"+" | "-"> }

MemberExpression { expression !member "[" expression? "]" }

BinaryExpression {
	expression !rel relation expression |
	expression !exp ArithOp<"**"> expression |
	expression !times (divide | ArithOp<"%"> | ArithOp<"*">) expression |
	expression !plus plusMin expression |
	expression !cmp CompareOp<"<" "="? | ">" "="? | "=" "="? "="? | "!=" "="?> expression |
	expression !bit BitOp<"|" | "^" | "&"> expression
}

relation { @specialize[@name=Rel]<identifier, "in" | "like" | "matches" | "contains" | "rlike" | "regex" | "irlike"> }

@external tokens tokens from './tokens.js' {
	Callee,
	Func,
	VarName,
	DeprecatedVar,
	DisabledVar,
	GlobalVar,
	Num
}

@tokens {
	// String
	escape { "\\" _ }
	stringContentSingle { ![\\']+ }
	stringContentDouble { ![\\"]+ }

	// skip
	spaces { $[\u0009\u000b\u000c ]+ }
	newline { $[\r\n] }

	divide[@name=ArithOp] { "/" }
	@precedence { "/*", divide }

	identifier { @asciiLetter+ }
	@precedence { spaces, newline, identifier }

	ArithOp<expr> { expr }
	LogicOp<expr> { expr }
	BitOp<expr> { expr }
	CompareOp<expr> { expr }

	":="[@name=Equals]

	"(" ")" "[" "]" "," ";"
}
